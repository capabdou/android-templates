apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("signing.properties")
def keystoreProperties = new Properties()
def globalConfig = rootProject.extensions.getByName("ext")
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            // Remember to edit signing.properties to have the correct info for release build.
            storeFile file('../keystore_production')
            storePassword keystoreProperties['KEYSTORE_PASSWORD']
            keyPassword keystoreProperties['KEY_PASSWORD']
            keyAlias keystoreProperties['KEY_ALIAS']
        }
    }

    compileSdkVersion globalConfig["android_compile_sdk_version"]
    defaultConfig {
        applicationId "co.nimblehq.android"
        minSdkVersion globalConfig["android_min_sdk_version"]
        targetSdkVersion globalConfig["android_target_sdk_version"]
        versionCode globalConfig["android_version_code"]
        versionName globalConfig["android_version_name"]
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            // For quickly testing build with proguard, enable this
            minifyEnabled false
        }
    }

    flavorDimensions "stage"
    productFlavors {
        production {
            applicationId "co.nimblehq"
        }

        staging {
            applicationId "co.nimblehq.staging"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {

    implementation(
        fileTree(dir: 'libs', include: ['*.jar']),

        "androidx.appcompat:appcompat:$android_support_version",
        "androidx.constraintlayout:constraintlayout:$constraint_layout_version",
        "androidx.core:core-ktx:$androidx_core_ktx_version",

        "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
        "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    )

    testImplementation(
        "junit:junit:$test_junit_version",
    )

    androidTestImplementation(
        "androidx.test.ext:junit:$android_test_junit_version",
        "androidx.test.espresso:espresso-core:$test_espresso_version"
    )
}
