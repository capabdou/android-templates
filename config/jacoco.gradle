apply plugin: 'jacoco'

jacoco {
    toolVersion = "$jacoco_core_version"
    reportsDir = file("$buildDir/reports/jacoco")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    dependsOn ":app:testStagingDebugUnitTest"
    dependsOn ":data:testStagingDebugUnitTest"

    def fileGenerated = ['**/R.class',
                         '**/R$*.class',
                         '**/*$ViewBinder*.*',
                         '**/*$InjectAdapter*.*',
                         '**/*Injector*.*',
                         '**/BuildConfig.*',
                         '**/Manifest*.*',
                         '**/*_ViewBinding*.*',
                         '**/*Adapter*.*',
                         '**/*Test*.*',
                         'android/**/*.*']

    def packagesExcluded = ['com/nimbl3/di/**',
                            'com/nimbl3/ui/**/di/**',
                            'com/nimbl3/extension/**',
                            'com/nimbl3/**/extension/**']
    // def filesExcluded = [ ]
    // def fileFilter = fileGenerated + packagesExcluded + filesExcluded
    def fileFilter = fileGenerated + packagesExcluded

    classDirectories.from = fileTree(
        dir: "$project.rootDir/app/build/intermediates/javac/stagingDebug/compileStagingDebugJavaWithJavac/classes",
        excludes: fileFilter
    ) + fileTree(
        dir: "$project.rootDir/data/build/intermediates/javac/stagingDebug/compileStagingDebugJavaWithJavac/classes",
        excludes: fileFilter
    ) + fileTree(
        dir: "$project.rootDir/app/build/tmp/kotlin-classes/stagingDebug",
        excludes: fileFilter
    ) + fileTree(
        dir: "$project.rootDir/data/build/tmp/kotlin-classes/stagingDebug",
        excludes: fileFilter
    )

    sourceDirectories.from = files([
        "$project.rootDir/app/src/main/java",
        "$project.rootDir/data/src/main/java"])

    executionData.from = fileTree(dir: project.rootDir, includes: [
        'app/build/jacoco/testStagingDebugUnitTest.exec',
        'data/build/jacoco/testStagingDebugUnitTest.exec'])

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
    }
}
